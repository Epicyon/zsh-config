# vim: ft=zsh:
prompt_minimal_precmd() {
  local branch dirty

  set -g git_info
  set -g ruby_info
  set -g host_info

  if branch="${$(git symbolic-ref -q HEAD 2>/dev/null)##refs/heads/}"; then
    git diff --quiet || dirty=' *'
    git_info=" %B%F{cyan}(%F{red}$branch%F{cyan})%F{green}$dirty%f%b"
  else
    unset git_info
  fi

  if (( $+commands[ruby] )); then
    ruby_info="%B%F{black}$(ruby --version | cut -d' ' -f 1-2)%f%b"
  else
    unset ruby_info
  fi

  if [[ -n "$SSH_CONNECTION" ]]; then
    host_info="%F{cyan}%m%f//"
  else
    unset host_info
  fi
}

prompt_minimal_setup() {
  autoload -Uz add-zsh-hook
  add-zsh-hook precmd prompt_minimal_precmd

  PROMPT=' $host_info%F{cyan}%c%f$git_info '
  RPROMPT='%(?::%B%F{red}%? %f%b)$ruby_info'

  prompt_opts=( cr percent subst )
}

prompt_minimal_setup "$@"
